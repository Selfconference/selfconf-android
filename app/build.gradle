// Manifest version information!
def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.selfconference.android"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            def prop = loadProperties('release-keystore.properties')
            storeFile rootProject.file('release.jks')
            storePassword prop.storePassword
            keyAlias prop.keyAlias
            keyPassword prop.keyPassword
        }
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release

            def prop = loadProperties('release.properties')

            resValue "string", "app_name", "Self.conference"

            buildConfigField "String", "PARSE_APPLICATION_ID", parseApplicationId(prop)
            buildConfigField "String", "PARSE_CLIENT_ID", parseClientSecret(prop)
            buildConfigField "String", "SELF_CONFERENCE_API_ENDPOINT", "\"http://selfconference.org/api\""
        }
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug

            def prop = loadProperties('debug.properties')

            resValue "string", "app_name", "Self.conference Debug"

            buildConfigField "String", "PARSE_APPLICATION_ID", parseApplicationId(prop)
            buildConfigField "String", "PARSE_CLIENT_ID", parseClientSecret(prop)
            buildConfigField "String", "SELF_CONFERENCE_API_ENDPOINT", "\"http://selfconf-dev.herokuapp.com/api\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'

    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:3.0.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup:otto:1.3.7'

    compile 'com.google.code.gson:gson:2.3.1'
    compile 'joda-time:joda-time:2.7'

    compile 'com.jpardogo.materialtabstrip:library:1.0.8'

    compile 'com.google.guava:guava:18.0'

    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'io.reactivex:rxjava-string:0.22.0'

    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    testCompile 'org.robolectric:robolectric:2.4'
    testCompile 'junit:junit:4.12'
    testCompile('com.squareup.assertj:assertj-android:1.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

def Properties loadProperties(String filename) {
    Properties properties = new Properties()
    if (rootProject.file(filename).exists()) {
        properties.load(new FileInputStream(rootProject.file(filename)))
    }
    return properties
}

def String parseApplicationId(Properties prop) {
    return "\"${prop.parseApplicationId ?: System.getenv('parseApplicationId')}\""
}

def String parseClientSecret(Properties prop) {
    return "\"${prop.parseClientSecret ?: System.getenv('parseClientSecret')}\""
}