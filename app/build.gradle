// Manifest version information!
def versionMajor = 2
def versionMinor = 0
def versionPatch = 2
def versionBuild = totalCommits()
// bump for dogfood builds, public betas, etc.

buildscript {
  repositories {
    maven { url "https://maven.fabric.io/public" }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$rootProject.deps.androidGradlePlugin"
    classpath "com.neenbedankt.gradle.plugins:android-apt:$rootProject.deps.androidApt"
    classpath "io.fabric.tools:gradle:$rootProject.deps.fabric"
    classpath "com.github.ben-manes:gradle-versions-plugin:$rootProject.deps.gradleVersionsPlugin"
    classpath "me.tatarka:gradle-retrolambda:3.3.0-beta4"
    classpath "me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2"
    classpath "com.fernandocejas.frodo:frodo-plugin:0.8.2"
  }

  // Exclude the lombok version that the android plugin depends on.
  configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

repositories {
  maven { url "https://maven.fabric.io/public" }
}

apply plugin: "com.android.application"
apply plugin: "io.fabric"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "com.fernandocejas.frodo"

apply from: "test-resources.gradle"

def totalCommits() {
  def p = 'git rev-list --count HEAD'.execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim().toInteger()
}

def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion rootProject.compileSdkVersion
  buildToolsVersion rootProject.buildToolsVersion

  dexOptions {
    // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isCi
  }

  defaultConfig {
    minSdkVersion rootProject.minSdkVersion
    targetSdkVersion rootProject.targetSdkVersion

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    signingConfig signingConfigs.debug

    vectorDrawables.useSupportLibrary = true
  }

  signingConfigs {
    release {
      def prop = loadProperties("release-keystore.properties")
      storeFile rootProject.file("release.jks")
      storePassword prop.storePassword
      keyAlias prop.keyAlias
      keyPassword prop.keyPassword
    }
    debug {
      storeFile rootProject.file("debug.keystore")
      storePassword "android"
      keyAlias "android"
      keyPassword "android"
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      signingConfig signingConfigs.release
    }
    debug {
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
    }
  }

  productFlavors {
    internal {
      applicationId "org.selfconference.android.internal"
    }
    production {
      applicationId "org.selfconference.android"
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude "META-INF/services/javax.annotation.processing.Processor"
  }

  lintOptions {
    checkReleaseBuilds false
  }

  testOptions.unitTests.all {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }
}

// TODO remove eventually: http://b.android.com/162285
configurations {
  internalDebugCompile
}

configurations.all {
  resolutionStrategy {
    force "com.android.support:support-annotations:$rootProject.deps.supportLibrary"
  }
}

dependencies {

  retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:2.3.0"

  compile "com.android.support:support-annotations:$rootProject.deps.supportLibrary"
  compile "com.android.support:appcompat-v7:$rootProject.deps.supportLibrary"
  compile "com.android.support:support-v4:$rootProject.deps.supportLibrary"
  compile "com.android.support:design:$rootProject.deps.supportLibrary"
  compile "com.android.support:recyclerview-v7:$rootProject.deps.supportLibrary"
  compile "com.android.support:cardview-v7:$rootProject.deps.supportLibrary"

  apt "com.ryanharter.auto.value:auto-value-parcel:$rootProject.deps.autoValueParcel"
  compile "com.ryanharter.auto.value:auto-value-parcel-adapter:$rootProject.deps.autoValueParcel"
  apt "com.ryanharter.auto.value:auto-value-moshi:$rootProject.deps.autoValueMoshi"
  apt "com.google.auto.value:auto-value:$rootProject.deps.autoValue"
  provided "com.jakewharton.auto.value:auto-value-annotations:$rootProject.deps.autoValueAnnotations"
  compile "com.jakewharton:butterknife:$rootProject.deps.butterknife"
  apt "com.jakewharton:butterknife-compiler:$rootProject.deps.butterknife"
  compile "com.jakewharton.timber:timber:$rootProject.deps.timber"
  compile "com.squareup.dagger:dagger:$rootProject.deps.dagger"
  apt "com.squareup.dagger:dagger-compiler:$rootProject.deps.dagger"
  compile "com.squareup.picasso:picasso:$rootProject.deps.picasso"
  compile "com.squareup.okhttp3:okhttp:$rootProject.deps.okhttp"
  internalCompile "com.squareup.okhttp3:logging-interceptor:$rootProject.deps.okhttp"
  compile "com.squareup.retrofit2:retrofit:$rootProject.deps.retrofit"
  compile "com.squareup.retrofit2:converter-moshi:$rootProject.deps.retrofit"
  compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.deps.retrofit"
  internalCompile "com.squareup.retrofit2:retrofit-mock:$rootProject.deps.retrofit"

  compile 'com.jakewharton.byteunits:byteunits:0.9.1'

  compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
  compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

  compile "com.squareup.moshi:moshi:$rootProject.deps.moshi"
  compile "com.jakewharton.threetenabp:threetenabp:$rootProject.deps.threeTenAbp"

  compile "com.google.guava:guava:$rootProject.deps.guava"

  compile "io.reactivex:rxjava:$rootProject.deps.rxJava"
  compile "io.reactivex:rxandroid:$rootProject.deps.rxJavaAndroid"
  compile "com.f2prateek.rx.preferences:rx-preferences:$rootProject.deps.rxPreferences"
  compile "com.trello:rxlifecycle:$rootProject.deps.rxLifecycle"
  compile "com.trello:rxlifecycle-components:$rootProject.deps.rxLifecycle"

  compile("com.crashlytics.sdk.android:crashlytics:$rootProject.deps.crashlytics") {
    transitive = true;
  }

  debugCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.deps.leakCanary"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.deps.leakCanary"

  internalCompile "com.mattprecious.telescope:telescope:$rootProject.deps.telescope"
  internalDebugCompile "com.jakewharton:process-phoenix:$rootProject.deps.processPhoenix"

  testCompile "org.robolectric:robolectric:$rootProject.deps.robolectric"
  testCompile "junit:junit:$rootProject.deps.junit"
  testCompile "com.squareup.assertj:assertj-android:$rootProject.deps.assertjAndroid"
}

if (!isCi) {
  def installAll = tasks.create("installAll")
  installAll.description = "Install all applications."
  android.applicationVariants.all { variant ->
    installAll.dependsOn(variant.install)
    // Ensure we end up in the same group as the other install tasks.
    installAll.group = variant.install.group
  }
}

// The default 'assemble' task only applies to normal variants. Add test variants as well.
android.testVariants.all { variant ->
  tasks.getByName("assemble").dependsOn variant.assemble
}

def Properties loadProperties(String filename) {
  Properties properties = new Properties()
  if (rootProject.file(filename).exists()) {
    properties.load(new FileInputStream(rootProject.file(filename)))
  }
  return properties
}
